{//;##########################packages##############################}
{//;Package search commands}
{if;{userhasrole;428637054922915850};
{if;{channelid};!=;479497706234970115;
{if;{messagetext};startswith;apt{space}search{space};{exec;searcher;apt {args;2;n}}{return}}
{if;{messagetext};startswith;apt-get{space}search{space};{exec;searcher;apt-get {args;2;n}}{return}}
{if;{messagetext};startswith;dnf{space}search{space};{exec;searcher;dnf {args;2;n}}{return}}
{if;{messagetext};startswith;zyp{space}search{space};{exec;searcher;zyp {args;2;n}}{return}}
{if;{messagetext};startswith;zypper{space}search{space};{exec;searcher;zyp {args;2;n}}{return}}
{if;{messagetext};startswith;emerge{space}--search{space};{exec;ddg;!genpkg {args;2;n}}{return}}
{if;{messagetext};startswith;pacman{space}-Ss{space};{exec;pacman;{args;2;n}}{return}}
{//;##########################sandwich##############################}
{//;Sandwich easter egg}
{if;{messagetext};==;sudo make me a sandwich;{reactadd;{channelid};{messageid};üçî}{return}}
}}
{//;################################################################}
{//;Everything below here ignores the sudoers role entirely}
{if;{userhasrole;457281742844330004};{return}}
{//;##########################slowmode##############################}
{//;enables slow mode for 60 seconds if a user has sent 5 or more messages in 5 seconds}
{//;enables slow mode for 120 seconds last 5 {messagetext}s within 10 seconds in {channelid} match}
{//;ignore #welcome}
{if;{channelid};!=;479497706234970115;
{//;if _{channelid}autoslowmode variable doesn't exist, creat it}
{if;{get;_{channelid}autoslowmode};==;;{set;_{channelid}autoslowmode;disabled}}
{//;set last user cap check time and cap if captime not found}
{if;{get;_{userid}captime};==;;
{set;_{userid}captime;{time;X}}
{commit}
{set;_{userid}cap;0}
{commit}}
{//;set last {channelid}global cap check time and cap if captime not found}
{if;{get;_{channelid}globalcaptime};==;;
{set;_{channelid}globalcaptime;{time;X}}
{commit}
{set;_{channelid}globalmsglast;{messagetext}}
{commit}
{set;_{channelid}globalcap;0}
{commit}}
{//;if last user cap check time was more than 5 seconds ago, reset cap variables}
{if;{get;_{userid}captime};<=;{math;-;{time;X};5};
{set;_{userid}captime;{time;X}}
{commit}
{set;_{userid}cap;0}
{commit}}
{//;if last cap {channelid}global check time was more than 10 seconds ago, reset cap variables}
{if;{get;_{channelid}globalcaptime};<=;{math;-;{time;X};10};
{set;_{channelid}globalcaptime;{time;X}}
{commit}
{set;_{channelid}globalmsglast;{messagetext}}
{commit}
{set;_{channelid}globalcap;0}
{commit}}
{//;if last user message was less than 5 seconds ago, add 20 to user's cap}
{//;cap is increased by 20 each time so that 5 messages will trigger slowmode}
{if;{get;_{channelid}autoslowmode};==;disabled;
{if;{messagetime;X};>=;{math;-;{time;X};5};
{set;_{userid}cap;{math;+;{get;_{userid}cap};20}}
{commit}
{set;_{userid}captime;{time;X}}
{commit}}}
{//;if last {channelid}global message was same as previous message, add 20 to gllobal cap}
{//;cap is increased by 20 each time so that 5 messages will trigger slowmode}
{if;{get;_{channelid}autoslowmode};==;disabled;
{if;{messagetext};==;{get;_{channelid}globalmsglast};
{set;_{channelid}globalmsglast;{messagetext}}
{commit}
{set;_{channelid}globalcap;{math;+;{get;_{channelid}globalcap};20}}
{commit}
{set;_{channelid}globalcaptime;{time;X}}
{commit}}}
{//;if user has reached 100 cap, turn on slow mode for 60 seconds and reset variables}
{if;{get;_{channelid}autoslowmode};==;disabled;
{if;{get;_{userid}cap};>=;100;
{set;_{userid}cap;0}
{set;_{channelid}globalcap;0}
{commit}
{//;send embed saying who triggered slowmode.  sending the embed also turns on slowmode}
{set;~msgid;{send;{channelid};{slowmode;{channelid};15}
{set;_{channelid}autoslowmode;enabled};
{embedbuild;
title:Slowmode enabled for 60 seconds!;
thumbnail.url:https://cdn.discordapp.com/emojis/478322841083969558.png?v=1;
color:D76F68;
description:{usermention} has triggered slowmode!;}}}
{//;sleep is broken in autoresponses apparently?}
{//;seems to be 5 seconds regardless of time set, so we sleep 12 times lol}
{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}
{//;turn off slowmode}
{slowmode}
{set;_{channelid}autoslowmode;disabled}
}}
{//;if {channelid}global has reached 100 cap, turn on slowmode for 120 seconds and reset variables}
{if;{get;_{channelid}autoslowmode};==;disabled;
{if;{get;_{channelid}globalcap};>=;100;
{set;_{channelid}globalcap;0}
{set;_{userid}cap;0}
{commit}
{//;send embed saying why slowmode triggered.  sending the embed also turns on slowmode}
{set;~msgid;{send;{channelid};{slowmode;{channelid};20}
{set;_{channelid}autoslowmode;enabled};
{embedbuild;
title:Slowmode enabled for 2 minutes!;
thumbnail.url:https://cdn.discordapp.com/emojis/478322841083969558.png?v=1;
color:D76F68;
description:Slowmode enabled for repeated message spam!;}}}
{//;sleep is broken in autoresponses apparently?}
{//;seems to be 5 seconds regardless of time set, so we sleep 24 times lol}
{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}{sleep;5s}
{//;turn off slowmode}
{slowmode}
{set;_{channelid}autoslowmode;disabled}
}}}
{//;##########################welcome###############################}
{//;welcome channel stuffs}
{if;{channelid};==;479497706234970115;
  {switch;{messagetext};
    {//;passphrase for welcome channel}
    ["sudo discord-linux --accept <#172522057291005953> --request users --code {get;_{userid}passcode}"];
    {//;do nothing and let discord-linux command handle this}
    {sleep;0.5s};
    {//;delete any other messages and output that incorrect passphrase was entered}
    {delete}
    {//;message auto deletes after 15 seconds}
    {set;~wrongpassid;{send;{channelid};The passphrase you entered was not correct. Please double-check the passphrase before sending it, {usermention}!}}
    {sleep;15s}
    {delete;{get;~wrongpassid}}}}
{set;_{userid}lastmessage;{time;X}}
{//;##########################support###############################}
{//;support channel stuffs}
{//;This section probably needs to stay near the bottom due to users role checks}
{//;If user does not have users role, check if they have chatted in support channels.  If not, output message telling them to check the pins}
{if;{channelid};==;96230043209981952;
{if;{get;_{userid}usedsupportlinux};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#96230043209981952>!  Please see the pinned messages and follow the template when posting here so that others may get right to helping you.;}}}
{set;_{userid}usedsupportlinux;true}}}
{if;{channelid};==;484513714024874018;
{if;{get;_{userid}usedsupportinstalls};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#484513714024874018>!  Please see the pinned messages and follow the template when posting here so that others may get right to helping you.;}}}
{set;_{userid}usedsupportinstalls;true}}}
{if;{channelid};==;484512261382340638;
{if;{get;_{userid}usedsupportgames};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#484512261382340638>!  Please see the pinned messages and follow the template when posting here so that others may get right to helping you.;}}}
{set;_{userid}usedsupportgames;true}}}
{if;{channelid};==;162712517053775872;
{if;{get;_{userid}usedsupportdiscord};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#162712517053775872>!  Please see the pinned messages and follow the template when posting here so that others may get right to helping you.;}}}
{set;_{userid}usedsupportdiscord;true}}}
{if;{channelid};==;484513481802907669;
{if;{get;_{userid}usedsupportother};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#484513481802907669>!  Please see the pinned messages and follow the template when posting here so that others may get right to helping you.;}}}
{set;_{userid}usedsupportother;true}}}
{//;##########################bot-spam##############################}
{//;bot-spam stuffs}
{//;This section probably needs to stay near the bottom due to users role checks}
{//;If user does not have users role and has not chatted in bot-spam, send info message}
{if;{channelid};==;174317464337776640;
{if;{quiet}{userhasrole;428637054922915850};{return}}
{if;{get;_{userid}usedbotspam};!=;true;
{void;{send;{channelid};{usermention};{embedbuild;
title:Read Me!;
color:C7A031;
description:This seems to be your first time in <#174317464337776640>!  Please see `sudo help` for a list of commands you can use until you've earned your `users` role.;}}}
{set;_{userid}usedbotspam;true}}}
{//;################################################################}