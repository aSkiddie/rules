{function;embedquote;
  {//;use {messageembeds} to find embedded elements from message above and store them}
  {set;~title;{jget;{messageembeds;{get;~c};{get;~m}};0.title}}
  {set;~url;{jget;{messageembeds;{get;~c};{get;~m}};0.url}}
  {set;~thumbnail;{jget;{messageembeds;{get;~c};{get;~m}};0.thumbnail.url}}
  {set;~desc;{jget;{messageembeds;{get;~c};{get;~m}};0.description}}
  {//;detect if elements not found and set them to something}
  {if;{get;~desc};startswith;`Cannot read property;{set;~desc;}}
  {if;{get;~title};startswith;`Cannot read property;{set;~title;Result for '{args}'}}
  {if;{get;~title};startswith;index.php;{set;~title;Result for '{args}'}}
  {if;{get;~thumbnail};startswith;`Cannot read property;{set;~thumbnail;{useravatar;{get;~user}}}}
  {if;{get;~url};startswith;`Cannot read property;{set;~url;https://discordapp.com/channels/{guildid}/{get;~c}/{get;~m}}}
  {if;{get;~url};==;;{set;~url;https://discordapp.com/channels/{guildid}/{get;~c}/{get;~m}}}
  {//;send copy of embed}
  {embed;{embedbuild;
    timestamp:{messagetime;{get;~c};{get;~m};YYYY-MM-DDTHH:mm:ssZ};
    color:{get;~eColor;0};
    title:{get;~title};
    url:{get;~url};
    thumbnail.url:{get;~thumbnail};
    description:{get;~desc};}}}

{set;~c;{channelid}}
{switch;{argslength};
	0;Usage: {newline}`{prefix}{commandname} [channel] <messageid>`{return};
    1;{set;~m;{args;0}};
    {set;~c;{args;0}}{set;~m;{args;1}}
}
{suppresslookup}
{set;~user;{messagesender;{get;~c};{get;~m}}}
{if;{get;~user};==;`No message found`;Invalid channel/messageID{return}}
{if;{logic;&&;{isnsfw;{get;~c}};{logic;!;{isnsfw}}};:x: Please use a nsfw channel!{return}}
{set;~eColor;[]}
{set;~roles;{roles;{get;~user}}}
{foreach;~color;{get;~roles};
	{if;{rolecolor;{get;~color}};!=;000000;
		{push;{get;~eColor};{rolecolor;{get;~color}}}
	}
}
{if;{length;{get;~eColor}};==;0;
	{set;~eColor;7087d9}
}
{if;{get;~msg};==;;{func.embedquote};
{set;~image;{shift;{messageattachments;{get;~c};{get;~m}}}}
{set;~msg;{regexreplace;{messagetext;{get;~c};{get;~m}};/(\[.+?\])(\(.+?\))/g;$1{zws}$2}}
{set;~url;
    https://discordapp.com/channels/
}
{embed;{embedbuild;
    footer.text:Sent by {username;{get;~user}}#{userdiscrim;{get;~user}};
    footer.icon_url:{useravatar;{get;~user}};
    title:Quote link;
    url:{get;~url}{guildid}/{get;~c}/{get;~m};
    {if;{length;{get;~image}};!=;0;image.url:{get;~image}};
    description:{if;{get;~msg};!=;;❝ {get;~msg} ❞};
    timestamp:{messagetime;{get;~c};{get;~m};YYYY-MM-DDTHH:mm:ssZ};
    color:{get;~eColor;0}
}}}