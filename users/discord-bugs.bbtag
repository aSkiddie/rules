{if;{guildid};==;96230004047740928;
{if;{channelid};!=;162712517053775872;{if;{channelid};!=;461299165100638228;{if;{channelid};!=;459245337119424512;{return}}}}}
{if;{flag;_};==;;{embed;{embedbuild;
description:**Usage**: `{prefix} {commandname} <search term> [flag <board to search>]`
Searches for Discord bugs using the Trello API.  The Linux board is used by default.  Use the `-b` flag to set a different Trello board to search.

Available boards:
**Linux**, **Desktop**, **Android**, **iOS**}}
{return}}
{if;{flagset;b};
{switch;{flag;b};
["desktop","Desktop"];{set;~board;AExxR9lU};
["android","Android"];{set;~board;Vqrkz3KO};
["ios","iOS"];{set;~board;vLPlnX60};
["other"];{if;{flag;c};==;;Missing board code!{return};{set;~board;{flag;c}}}
;{set;~board;UyU76Esh}};
{set;~board;UyU76Esh}}
{set;~request;{request;https://api.trello.com/1/boards/{get;~board}/cards?limit=200}}
{set;~length;{length;{jget;{get;~request};body}}}
{set;~query;{flag;_}}
{set;~found;false}
{for;~item;0;<;{get;~length};
{if;{inject;{lb}regextest{semi}{replace;{jget;{get;~request};body.{get;~item}.name};{semi};}{semi}/{get;~query}/gi{rb}};
{set;~found;true}{jget;{get;~request};body.{get;~item}.shortUrl}{newline}}
}
{if;{get;~found};==;false;
No results found for '{get;~query}'!
{return}}