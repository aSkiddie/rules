{if;{flag;_};==;;Missing manpage input!{return}}
{//; Set the array here }
{set;~raw;{request;http://man.he.net/?topic={uriencode;{flag;_}}&section=all}}
{set;~request;{regexreplace;{regexreplace;{jget;{get;~raw};body};/<.*>/g;};/^\s*\n/gm;}}
{if;{get;~request};startswith;Man Pages Copyright Respective Owners;
{embed;{embedbuild;
title:man for {args;0};
url:http://manpages.ubuntu.com/manpages/bionic/{uriencode;{flag;_}};
thumbnail.url:https://cdn.discordapp.com/emojis/478073442311077889.gif?v=1&size=40;color:2C8EBE;}}{return}}
{if;{get;~request};includes;Argument length exceeded limit;
{embed;{embedbuild;
title:man for {args;0};
url:http://manpages.ubuntu.com/manpages/bionic/{uriencode;{flag;_}};
thumbnail.url:https://cdn.discordapp.com/emojis/478073442311077889.gif?v=1&size=40;color:2C8EBE;}}{return}}
{set;~arr;{split;{get;~request};{space}}}
{//; Set max items per page }
{if;{flagset;l};{set;~items;{flag;l}};{set;~items;200}}
{if;{get;~items};>=;600;{set;~items;600}}
{//; Make sure args is a valid number }
{if;{flagset;p};{set;~p;{flag;p}};{set;~p;1}}
{//; Calculate the max page }
{set;~maxp;{ceil;{math;/;{length;{get;~arr}};{get;~items}}}}
{//; Make sure args doesn't exceed max pages }
{if;{get;~p};>;{get;~maxp};{set;~p;{get;~maxp}}}
{//; Make sure args isn't a negative }
{if;{get;~p};<=;0;{set;~p;1}}
{set;~title;Man Page for '{flag;_}' <http://man.he.net/?topic={uriencode;{flag;_}}&section=all>}
{function;list;
{//; Set the start and end of the slice }
{set;~s;{math;*;{math;-;{get;~p};1};{get;~items}}}
{set;~e;{math;+;{get;~s};{get;~items}}}
{//; Output of the array }
{get;~title}
{repeat;`;3}ml
{foreach;~man;{slice;{get;~arr};{get;~s};{get;~e}};
{get;~man}{space}
}{newline}--------------------
Page {get;~p}/{get;~maxp}
{repeat;`;3}
}
{set;~msgid;{output;{func.list}}}
{set;~used;0}
{if;{get;~maxp};>=;2;
{reactadd;{get;~msgid};⬅➡}
{repeat;
{//; Make sure only applicabl emojis work }
{set;~reaction;{waitreaction;{get;~msgid};{userid};⬅➡;true;300}}
{//; Remove emojis added by user }
{reactremove;{get;~msgid};⬅➡}
{//; Determine whether to add or decrease page }
{switch;{get;~reaction;3};
⬅;{set;~p;{math;-;{get;~p};1}};
➡;{set;~p;{math;+;{get;~p};1}}
}
{set;~used;{math;+;{get;~used};1}}
{if;{get;~p};<=;0;{set;~p;{get;~maxp}}}
{if;{get;~p};>;{get;~maxp};{set;~p;1}}
{//; Output the resulting page }
{if;{get;~used};>=;10;
{set;~title;Page switch limit reached.  Use `{prefix} {commandname} {flag;_} -p {math;+;{get;~p};1}` to continue.{newline}Man Page for '{flag;_}' <http://man.he.net/?topic={uriencode;{flag;_}}&section=all>}
{edit;{get;~msgid};{func.list}};
{edit;{get;~msgid};{func.list}}};
10
}
{//; Remove all emojis when waitreaction limit is reached }
{reactremove;{get;~msgid};{reactlist;{get;~msgid};{reactlist;{get;~msgid}}}}}