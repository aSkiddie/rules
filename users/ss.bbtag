{function;help;{dump;**This command only works in #customization!**

**Usage:** `{prefix} {commandname} [flag <user|image url|description>]`

Available flags:

```
username or userid
Display the users current desktop screenshot.
Note: try including the user's discrim if user is not found.

-r username or userid
Display the users current desktop screenshot rating.

-l
List all users who have stored screenshots.
The `-p` flag may be used to start at a specific rank in the list.
Ex: `sudo ss -l -p 11`
The `-b` flag will list screenshots in reverse order.

-s https://url.to/image.png
Set the url of your current *nix desktop screenshot.

-d Distro: distroname DE: DE name
Set the description of your current desktop screenshot.

:upvote: username or userid
Upvote the users current desktop screenshot.

:downvote: username or userid
Downvote the users current desktop screenshot.

unvote username or userid
Remove your vote from the users current desktop screenshot.
```

**https://ibin.co is recommended for permanent image hosting.**}}
{if;{flagset;(};{func.help}{return}}
{function;toplist;{suppresslookup}
{set;~users;[]}
{foreach;~user;{rolemembers;428637054922915850};
{if;{length;{get;@{get;~user}ssurl}};!=;0;
{if;{usermention;{get;~user}};==;;{//;left};
{push;~users;{realpad;{get;_{get;~user}ssrating};6;0;left}({get;~user})}}}}
{//; Sort rated screenshots }
{if;{flagset;b};{sort;~users};{sort;~users;descending}}
{set;~maxp;{length;{get;~users}}}}
{function;posttop;{foreach;~ssuser;
{if;{get;~p};<=;0;{set;~p;0}}
{if;{get;~p};>=;{get;~maxp};{get;~maxp}}
{match;{slice;{get;~users};{get;~p};{math;+;{get;~p};1}};/\d{17,21}/g};
{set;~eColor;[]}
{set;~roles;{roles;{get;~ssuser}}}
{foreach;~color;{get;~roles};
  {if;{rolecolor;{get;~color}};!=;000000;
    {push;{get;~eColor};{rolecolor;{get;~color}}}
  }
}
{if;{length;{get;~eColor}};==;0;
  {set;~eColor;c1694f}
}
{set;~currentss;{embedbuild;
title:{usernick;{get;~ssuser}}'s Desktop Screenshot;
color:{get;~eColor;0};
image.url:{get;@{get;~ssuser}ssurl};
footer.text:Rank {get;~p}/{math;-;{get;~maxp};1};
description:{if;{length;{get;@{get;~ssuser}ssdesc}};>;0;Description:
{regexreplace;{get;@{get;~ssuser}ssdesc};/[\*_]/g;}}
User Rating: {get;_{get;~ssuser}ssrating}
User ID: {get;~ssuser};}}
{commit}
}}
{if;{channelid};==;173927645334077441;
{suppresslookup}
{if;{argslength};==;0;
{if;{length;{get;@{userid}ssurl}};==;0;
{delete}
{set;~msgidns;{send;{channelid};You have no *nix desktop screenshot url stored {usermention}!
See this link for more info: {func.help}}}
{timer;{void;{delete;{get;~msgidns}}};10s}
{return};
{exec;ssvault;-r{space}{userid}}
{return}}}
{if;{flagset;h};
{delete}
{set;~msgidh;{send;{channelid};See this link for more info: {func.help} {usermention}!}}
{timer;{void;{delete;{get;~msgidh}}};10s}
{return}}
{if;{flagset;%};
{delete}
{embed;{embedbuild;
title:{prefix} {commandname};
color:6F86D8;
description:**Usage:** `{prefix} {commandname} [flag <user|image url|description>]`{newline}
Available flags:{newline}
*username or userid*
Display the user's current desktop screenshot.
*Note: try including the user's discrim if user is not found.*{newline}
*-r username or userid*
Display the user's current desktop screenshot rating.{newline}
*-l*
List all users who have stored screenshots.
The `-p` flag may be used to start at a specific rank in the list.
Ex: `sudo ss -l -p 11`
The `-b` flag will list screenshots in reverse order.{newline}
*-s https://url.to/image.png*
Set the url of your current *nix desktop screenshot.{newline}
*-d Distro: distroname DE: DE name*
Set the description of your current desktop screenshot.{newline}
*<:upvote:466042779273920514> username or userid*
Upvote the user's current desktop screenshot.{newline}
*<:downvote:466042848454770730> username or userid*
Downvote the user's current desktop screenshot.{newline}
*unvote username or userid*
Remove your vote from the user's current desktop screenshot.{newline}
**<https://ibin.co> is recommended for permanent image hosting.**}}
{return}}
{if;{flagset;s};
{delete}
{if;{length;{flag;s}};==;0;
{set;~msgidns;{send;{channelid};Missing screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidns}}};5s}
{return}}
{if;{flag;s};==;{get;@{userid}ssurl};
{set;~msgidss;{send;{channelid};<{flag;s}> is the same as the currently stored URL {usermention}!}}
{timer;{void;{delete;{get;~msgidss}}};5s}
{return}}
{if;{flag;s};includes;png;
{set;_{userid}ssrating;0}
{set;@{userid}ssurl;{flag;s}};
{set;~msgidpng;{send;{channelid};Sorry, only .png images are accepted {usermention}!}}
{timer;{void;{delete;{get;~msgidpng}}};5s}
{return}}}
{if;{flagset;d};
{if;{flagset;s};;{delete}}
{if;{length;{get;_{userid}ssrating}};==;0;
{set;_{userid}ssrating;0}}
{set;@{userid}ssdesc;{flag;d}}}
{if;{flagset;s};{usernick} changed their desktop screenshot!
{exec;ssvault;-u{space}{userid}}
{return};
{if;{flagset;d};{usernick} changed the description for their screenshot!
{exec;ssvault;-u{space}{userid}}}}
{if;{flagset;d};{return}}
{if;{flagset;l};
{if;{flagset;p};{set;~p;{flag;p}};{set;~p;0}}
{func.toplist}
{func.posttop}
{set;~msgid;{send;{channelid};{zws};{get;~currentss}}}
{reactadd;{get;~msgid};⬅➡}
{repeat;
{//; Make sure only applicabl emojis work }
{set;~reaction;{waitreaction;{get;~msgid};{userid};⬅➡;true;300}}
{//; Remove emojis added by user }
{reactremove;{get;~msgid};⬅➡}
{//; Determine whether to add or decrease page }
{switch;{get;~reaction;3};
⬅;{set;~p;{math;-;{get;~p};1}}{commit}
{func.posttop}
{edit;{get;~msgid};{zws};{get;~currentss}};
➡;{set;~p;{math;+;{get;~p};1}}{commit}
{func.posttop}
{edit;{get;~msgid};{zws};{get;~currentss}}
};
10
}
{//; Remove all emojis when waitreaction limit is reached }
{reactremove;{get;~msgid};{reactlist;{get;~msgid};{reactlist;{get;~msgid}}}}
{return}}
{if;{flagset;r};
{delete}
{if;{userid;{flag;r};quiet};==;;
{set;~msgidu;{send;{channelid};❌ '{flag;r}' not found in {guildname}}}
{timer;{void;{delete;{get;~msgidu}}};5s}
{return};
{set;~ssuser;{userid;{flag;r}}}}
{if;{length;{get;_{get;~ssuser}ssrating}};==;0;
{set;_{get;~ssuser}ssrating;0}}
{if;{length;{get;@{get;~ssuser}ssurl}};==;0;
{set;~msgino;{send;{channelid};{usernick;{get;~ssuser}} has not set a screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidno}}};5s}
{return}}
{set;~msgidr;{send;{channelid};{if;{get;~ssuser};==;{userid};Your;{usernick;{get;~ssuser}}'s} desktop screenshot rating is {get;_{get;~ssuser}ssrating} {usermention}!}}
{timer;{void;{delete;{get;~msgidr}}};5s}
{return}}
{if;{flagset;u};
{if;{userid;{flag;u};quiet};==;;
{set;~msgidu;{send;{channelid};❌ '{flag;u}' not found in {guildname}}}
{timer;{void;{delete;{get;~msgidu}}};5s}
{return};
{set;~ssuser;{userid;{flag;u}}}}
{if;{length;{get;_{get;~ssuser}ssrating}};==;0;
{set;_{get;~ssuser}ssrating;0}}
{if;{length;{get;@{get;~ssuser}ssurl}};==;0;
{set;~msgino;{send;{channelid};{usernick;{get;~ssuser}} has not set a screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidno}}};5s}
{return}}
{set;~eColor;[]}
{set;~roles;{roles;{get;~ssuser}}}
{foreach;~color;{get;~roles};
  {if;{rolecolor;{get;~color}};!=;000000;
    {push;{get;~eColor};{rolecolor;{get;~color}}}
  }
}
{if;{length;{get;~eColor}};==;0;
  {set;~eColor;c1694f}
}
{embed;{embedbuild;
title:{usernick;{get;~ssuser}}'s Desktop Screenshot;
color:{get;~eColor;0};
image.url:{get;@{get;~ssuser}ssurl};
description:{if;{length;{get;@{get;~ssuser}ssdesc}};>;0;__Description__:
{get;@{get;~ssuser}ssdesc}}
__User Rating__: {get;_{get;~ssuser}ssrating}
__User ID__: {get;~ssuser};}}
{return}}
{switch;{args;0};
["unvote","Unvote"];
{delete}
{if;{userid;{args;1;n};quiet};==;;
{set;~msgidf;{send;{channelid};❌ '{args;1;n}' not found in {guildname}}}
{timer;{void;{delete;{get;~msgidf}}};5s}
{return};
{set;~ssuser;{userid;{args;1;n}}}}
{if;{get;~ssuser};==;{userid};
{set;~msgidy;{send;{channelid};You cannot vote on yourself {usermention}!}}
{timer;{void;{delete;{get;~msgidy}}};5s}
{return}}
{if;{length;{get;@{get;~ssuser}ssurl}};==;0;
{set;~msgidu;{send;{channelid};{usernick;{get;~ssuser}} has not set a screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidu}}};5s}
{return}}
{if;{get;_{userid}up{get;@{get;~ssuser}ssurl}};==;true;
{set;_{get;~ssuser}ssrating;{math;-;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}up{get;@{get;~ssuser}ssurl};false}
Removed {usernick}'s upvote from {usernick;{get;~ssuser}}'s current screenshot.}
{if;{get;_{userid}down{get;@{get;~ssuser}ssurl}};==;true;
{set;_{get;~ssuser}ssrating;{math;+;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}down{get;@{get;~ssuser}ssurl};false}
Removed {usernick}'s downvote from {usernick;{get;~ssuser}}'s current screenshot.}
{set;~msgidcr;{send;{channelid};{usernick;{get;~ssuser}}'s desktop screenshot rating is now {get;_{get;~ssuser}ssrating}!}}
{timer;{void;{delete;{get;~msgidcr}}};5s}
{return};
["upvote","<:upvote:466042779273920514>"];
{delete}
{if;{userid;{args;1;n};quiet};==;;
{set;~msgidu;{send;{channelid};❌ '{args;1;n}' not found in {guildname}}}
{timer;{void;{delete;{get;~msgidu}}};5s}
{return};
{set;~ssuser;{userid;{args;1;n}}}}
{if;{get;~ssuser};==;{userid};
{set;~msgidy;{send;{channelid};You cannot vote on yourself {usermention}!}}
{timer;{void;{delete;{get;~msgidy}}};5s}
{return}}
{if;{length;{get;@{get;~ssuser}ssurl}};==;0;
{set;~msgidns;{send;{channelid};{usernick;{get;~ssuser}} has not set a screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidns}}};5s}
{return}}
{if;{get;_{userid}up{get;@{get;~ssuser}ssurl}};==;true;
{set;~msgidau;{send;{channelid};You already upvoted {usernick;{get;~ssuser}} {usermention}!}}
{timer;{void;{delete;{get;~msgidau}}};5s}
{return}}
{if;{length;{get;_{get;~ssuser}ssrating}};==;0;
{set;_{get;~ssuser}ssrating;0}}
{set;_{get;~ssuser}ssrating;{math;+;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}up{get;@{get;~ssuser}ssurl};true}
{if;{get;_{userid}down{get;@{get;~ssuser}ssurl}};==;true;
{set;_{get;~ssuser}ssrating;{math;+;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}down{get;@{get;~ssuser}ssurl};false}}
{usernick} upvoted {usernick;{get;~ssuser}}!  {usernick;{get;~ssuser}}'s desktop screenshot rating is now {get;_{get;~ssuser}ssrating}!
{return};
["downvote","<:downvote:466042848454770730>"];
{delete}
{if;{userid;{args;1;n};quiet};==;;
{set;~msgidu;{send;{channelid};❌ '{args;1;n}' not found in {guildname}}}
{timer;{void;{delete;{get;~msgidu}}};5s}
{return};
{set;~ssuser;{userid;{args;1;n}}}}
{if;{get;~ssuser};==;{userid};
{set;~msgidy;{send;{channelid};You cannot vote on yourself {usermention}!}}
{timer;{void;{delete;{get;~msgidy}}};5s}
{return}}
{if;{length;{get;@{get;~ssuser}ssurl}};==;0;
{set;~msgidns;{send;{channelid};{usernick;{get;~ssuser}} has not set a screenshot URL {usermention}!}}
{timer;{void;{delete;{get;~msgidns}}};5s}
{return}}
{if;{get;_{userid}down{get;@{get;~ssuser}ssurl}};==;true;
{set;~msgidad;{send;{channelid};You already downvoted {usernick;{get;~ssuser}} {usermention}!}}
{timer;{void;{delete;{get;~msgidad}}};5s}
{return}}
{if;{length;{get;_{get;~ssuser}ssrating}};==;0;
{set;_{get;~ssuser}ssrating;0}}
{set;_{get;~ssuser}ssrating;{math;-;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}down{get;@{get;~ssuser}ssurl};true}
{if;{get;_{userid}up{get;@{get;~ssuser}ssurl}};==;true;
{set;_{get;~ssuser}ssrating;{math;-;{get;_{get;~ssuser}ssrating};1}}
{set;_{userid}up{get;@{get;~ssuser}ssurl};false}}
{usernick} downvoted {usernick;{get;~ssuser}}!  {usernick;{get;~ssuser}}'s desktop screenshot rating is now {get;_{get;~ssuser}ssrating}!
{set;~msgiddv;{send;{channelid};Please give some ***constructive*** criticism on {usernick;{get;~ssuser}}'s desktop screenshot {usermention}!}}
{timer;{void;{delete;{get;~msgiddv}}};5s}
{return};
{if;{userid;{args};quiet};==;;
{delete}
{set;~msgidhp;{send;{channelid};❌ '{args;1;n}' not found in {guildname}
See the pinned messages for how to use `{prefix} {commandname}` {usermention}!}}
{timer;{void;{delete;{get;~msgidhp}}};5s}
{return};
{set;~ssuser;{userid;{args}}}
{exec;ssvault;-u{space}{get;~ssuser}}}};
{if;{channelid};==;174317464337776640;
**This command only works in <#173927645334077441>!**
See this link for more info: {func.help}
{return};
`{prefix} {commandname}` must be used in <#173927645334077441> {usermention}!}}